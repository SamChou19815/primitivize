use std::str::FromStr;
use crate::ast::{
  LiteralValue,
  BinaryOperator,
  ExpressionStaticType,
  SourceLanguageExpression,
  SourceLanguageMutableGlobalVariableDefinition,
  SourceLanguageFunctionDefinition,
  SourceLanguageProgram,
};

grammar;

Comma<T>: Vec<T> = { // (1)
  <v:(<T> ",")*> <e:T?> => match e { // (2)
      None => v,
      Some(e) => {
          let mut v = v;
          v.push(e);
          v
      }
  }
};

Semicolon<T>: Vec<T> = { // (1)
  <v:(<T> ";")*> <e:T?> => match e { // (2)
      None => v,
      Some(e) => {
          let mut v = v;
          v.push(e);
          v
      }
  }
};

Num: i32 = r"[0-9]+" => i32::from_str(<>).unwrap();

LiteralValue: LiteralValue = {
  r"[0-9]+" => LiteralValue::IntLiteral(i32::from_str(<>).unwrap()),
  "true" => LiteralValue::BoolLiteral(true),
  "false" => LiteralValue::BoolLiteral(false),
}

ExpressionStaticType: ExpressionStaticType = {
  "void" => ExpressionStaticType::VoidType,
  "int" => ExpressionStaticType::IntType,
  "bool" => ExpressionStaticType::BoolType,
}

Identifier: String = r"[a-z][A-Za-z0-9]*" => <>.to_string();

SimpleExpression : Box<SourceLanguageExpression> = {
  <l:@L> <literal:LiteralValue> => Box::new(SourceLanguageExpression::LiteralExpression {
    line_number: l,
    literal: literal,
  }),
  <l:@L> <identifier: Identifier> => Box::new(SourceLanguageExpression::VariableExpression {
    line_number: l,
    identifier,
  }),
  "(" <e: SourceLanguageExpression> ")" => e,
}

FunctionCallExpression : Box<SourceLanguageExpression> = {
  <l:@L> <function_name: Identifier> "("
  <function_arguments: Comma<SourceLanguageExpression>>
  ")" => Box::new(SourceLanguageExpression::FunctionCallExpression {
    line_number: l,
    static_type: ExpressionStaticType::VoidType,
    function_name,
    function_arguments,
  }),
  SimpleExpression,
}

FactorOp: BinaryOperator = {
  "*" => BinaryOperator::MUL,
  "/" => BinaryOperator::DIV,
  "%" => BinaryOperator::MOD,
}

FactorExpression : Box<SourceLanguageExpression> = {
  <l:@L> <e1: FactorExpression> <op: FactorOp> <e2: FunctionCallExpression>
  => Box::new(SourceLanguageExpression::BinaryExpression {
    line_number: l,
    operator: op,
    e1,
    e2,
  }),
  FunctionCallExpression,
}

TermOp: BinaryOperator = {
  "+" => BinaryOperator::PLUS,
  "-" => BinaryOperator::MINUS,
}

TermExpression : Box<SourceLanguageExpression> = {
  <l:@L> <e1: TermExpression> <operator: TermOp> <e2: FactorExpression>
  => Box::new(SourceLanguageExpression::BinaryExpression {
    line_number: l,
    operator,
    e1,
    e2,
  }),
  FactorExpression,
}

ComparisonOp: BinaryOperator = {
  "<" => BinaryOperator::LT,
  "<=" => BinaryOperator::LE,
  ">" => BinaryOperator::GT,
  ">=" => BinaryOperator::GE,
  "==" => BinaryOperator::EQ,
  "!=" => BinaryOperator::NE,
}

ComparisonExpression : Box<SourceLanguageExpression> = {
  <l:@L> <e1: ComparisonExpression> <operator: ComparisonOp> <e2: TermExpression>
  => Box::new(SourceLanguageExpression::BinaryExpression {
    line_number: l,
    operator,
    e1,
    e2,
  }),
  TermExpression,
}

ConjunctionExpression : Box<SourceLanguageExpression> = {
  <l:@L> <e1: ConjunctionExpression> "&&" <e2: ComparisonExpression>
  => Box::new(SourceLanguageExpression::BinaryExpression {
    line_number: l,
    operator: BinaryOperator::AND,
    e1,
    e2,
  }),
  ComparisonExpression,
}

DisjunctionExpression : Box<SourceLanguageExpression> = {
  <l:@L> <e1: DisjunctionExpression> "||" <e2: ConjunctionExpression>
  => Box::new(SourceLanguageExpression::BinaryExpression {
    line_number: l,
    operator: BinaryOperator::OR,
    e1,
    e2,
  }),
  ConjunctionExpression,
}

IfElseExpression : Box<SourceLanguageExpression> = {
  <l:@L> "if" <condition: SourceLanguageExpression> "then"
    <e1: IfElseExpression>
  "else"
    <e2: IfElseExpression>
  => Box::new(SourceLanguageExpression::IfElseExpression {
    line_number: l,
    condition,
    e1,
    e2,
  }),
  DisjunctionExpression,
}

AssignExpression : Box<SourceLanguageExpression> = {
  <l:@L> <identifier: Identifier> "=" <assigned_expression: IfElseExpression>
  => Box::new(SourceLanguageExpression::AssignmentExpression {
    line_number: l,
    identifier,
    assigned_expression,
  }),
  IfElseExpression,
}

SourceLanguageExpression : Box<SourceLanguageExpression> = {
  <l:@L> "{" <expressions: Semicolon<SourceLanguageExpression>> "}"
  => Box::new(SourceLanguageExpression::ChainExpression {
    line_number: l,
    expressions,
  }),
  AssignExpression,
}

SourceLanguageMutableGlobalVariableDefinition : SourceLanguageMutableGlobalVariableDefinition = {
  <l:@L> "var" <identifier: Identifier> "=" <assigned_value: Num> ";"
  => SourceLanguageMutableGlobalVariableDefinition {
    line_number: l,
    identifier,
    assigned_value,
  }
};

FunctionArgument: (String, ExpressionStaticType) =
  <identifier: Identifier> ":" <t: ExpressionStaticType> => (identifier, t);

SourceLanguageFunctionDefinition : SourceLanguageFunctionDefinition = {
  <l:@L> "fun" <identifier: Identifier> "("
  <function_arguments: Comma<FunctionArgument>>
  ")" ":" <return_type: ExpressionStaticType> "=" <e: SourceLanguageExpression>
  => SourceLanguageFunctionDefinition {
    line_number: l,
    identifier,
    function_arguments,
    return_type,
    body: e,
  }
}

pub SourceLanguageProgram : Box<SourceLanguageProgram> = {
  <global_variable_definitions: SourceLanguageMutableGlobalVariableDefinition*>
  <function_definitions: SourceLanguageFunctionDefinition*>
  => Box::new(SourceLanguageProgram {
    global_variable_definitions,
    function_definitions,
  })
}
